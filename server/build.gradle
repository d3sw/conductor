buildscript {
    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0' // Updated for Gradle 5 compatibility
}

configurations.all {
    resolutionStrategy {
        force 'com.fasterxml.jackson.core:jackson-core:2.7.5'
        force 'com.fasterxml.jackson.core:jackson-databind:2.7.5'
    }
}

apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

dependencies {
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.apache.logging.log4j' && details.target.version < '2.17.0') {
                details.useVersion '2.17.0'
                details.because 'CVE-2021-44228'
            }
        }
    }

    // Log4j
    implementation 'org.apache.logging.log4j:log4j-core:2.9.0'
    implementation 'org.apache.logging.log4j:log4j-jul:2.9.0'
    implementation 'org.slf4j:jul-to-slf4j:1.7.25'

    // Conductor
    implementation project(':conductor-core')
    implementation project(':conductor-jersey')
    implementation project(':conductor-redis-persistence')
    implementation project(':conductor-contribs')
    implementation project(':conductor-es6rest-persistence')
    implementation project(':conductor-postgresql-persistence')

    // Jetty
    implementation 'org.eclipse.jetty:jetty-server:9.3.9.v20160517'
    implementation 'org.eclipse.jetty:jetty-servlet:9.3.9.v20160517'

    // Guice
    implementation 'com.sun.jersey.contribs:jersey-guice:1.19.4'
    implementation 'com.google.inject:guice:4.1.0'
    implementation 'com.google.inject.extensions:guice-servlet:4.1.0'

    // Swagger
    implementation 'io.swagger:swagger-jersey-jaxrs:1.5.0'

    // In memory
    implementation 'org.rarefiedredis.redis:redis-java:0.0.17'

    // JNA library for EmbeddedElasticSearch
    implementation 'net.java.dev.jna:jna:4.1.0'
}

shadowJar {
    mergeServiceFiles()
    configurations = [project.configurations.runtimeClasspath] // Use `runtimeClasspath` for Gradle 5 compatibility
    manifest {
        attributes 'Description': 'Self contained Conductor server jar'
        attributes 'Main-Class': 'com.netflix.conductor.server.Main'
    }
}

publishing {
    publications {
        nebula(MavenPublication) {
            artifact shadowJar
        }
    }
}

gretty {
    contextPath = '/'
    servletContainer = 'tomcat8'
    scanDirs = ['**/src/main/resources/**']
    scanDependencies = true
    port = 8080
}

configurations.grettyRunnerTomcat8 {
    exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

build.dependsOn('shadowJar')

task server(type: JavaExec) {
    systemProperty 'loadSample', 'true'
    systemProperty 'workflow.elasticsearch.url', 'localhost:9300'
    main = 'com.netflix.conductor.server.Main'
    classpath = sourceSets.test.runtimeClasspath
}
